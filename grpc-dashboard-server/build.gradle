plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.0'
	id 'io.spring.dependency-management' version '1.1.5'
	id 'com.google.protobuf' version '0.9.4'
}

group = 'com.demo'
version = '0.0.1'

def grpcVersion = '1.64.0' // CURRENT_GRPC_VERSION
def protocVersion = '4.27.0'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-services:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	implementation "io.grpc:grpc-netty:${grpcVersion}"
	implementation "com.google.protobuf:protobuf-java-util:${protocVersion}"
	implementation "com.google.protobuf:protobuf-java:${protocVersion}"

	// Generated Annotation 때문에 필요
	compileOnly "javax.servlet:javax.servlet-api:4.0.1"
	compileOnly "org.apache.tomcat:annotations-api:6.0.53"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" } }
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
